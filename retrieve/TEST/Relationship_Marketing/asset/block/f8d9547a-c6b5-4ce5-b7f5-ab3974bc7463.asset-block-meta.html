<table cellpadding="0" cellspacing="0" width="100%" role="presentation" style="min-width: 100%; " class="stylingblock-content-wrapper"><tr><td class="stylingblock-content-wrapper camarker-inner">
%%[
    VAR @retrieveFulfillmentRecord, @limitRow, @LeadId, @retrieveLeadRecord, @FirstName, @limitLeadRow
    /* Retrieve Caregiver FirstName and OwnerId from Lead record */
    SET @retrieveLeadRecord = RetrieveSalesforceObjects(
        "Lead",
        "Caregiver_First_Name__c, OwnerId",
        "Id",
        "=",
        LeadId
    )
    IF RowCount(@retrieveLeadRecord) >= 1 THEN
        SET @limitLeadRow = Row(@retrieveLeadRecord, 1)
        SET @FirstName = Field(@limitLeadRow, "Caregiver_First_Name__c")
        SET @OwnerID = Field(@limitLeadRow, "OwnerId")
        IF Empty(@FirstName) THEN
            SET @FirstName = "Caregiver"
        ENDIF
    ELSEInsertDE(
            "TriggeredSendErrorLog",
            "EmailName",
            emailname_,
            "Date",
            Now(),
            "Reason",
            "No Lead Record Found",
            "SubscriberKey",
            _SubscriberKey
        )
        RaiseError("No Lead Record Found", true)
    ENDIF
    /* Update Email Sent field on Fulfillment record */
    UpdateSingleSalesforceObject ('Fulfillment__c', FulfillmentID, 'Email_Sent__c', 'True', 'Email_Sent_Status__c', 'Email Sent')
    /* Retrieve Lead Owner Record */
    IF Substring(@OwnerID, 1, 3) == "005" THEN
        VAR @retrieveOwnerRecord, @limitOwnerRow, @OwnerName
        SET @retrieveOwnerRecord = RetrieveSalesforceObjects(
            "User",
            "Name,Phone,Email",
            "Id",
            "=",
            @OwnerID
        )
        SET @limitOwnerRow = Row(@retrieveOwnerRecord, 1)
        SET @OwnerName = Field(@limitOwnerRow, "Name")
        SET @OwnerNumber = Field(@limitOwnerRow, "Phone")
        SET @OwnerEmail = Field(@limitOwnerRow, "Email")
        SET @indexname = IndexOf(@OwnerName, "Queue")
        SET @substringname = Substring(@OwnerName, @indexname, length(@OwnerName))
        IF @substringname == "Queue" THEN
            InsertDE(
                "TriggeredSendErrorLog",
                "EmailName",
                emailname_,
                "Date",
                Now(),
                "Reason",
                "Lead Owner is Queue",
                "SubscriberKey",
                _SubscriberKey
            )
            RaiseError('Lead Owner is Queue', true)
        ENDIF
    ELSEInsertDE(
            "TriggeredSendErrorLog",
            "EmailName",
            emailname_,
            "Date",
            Now(),
            "Reason",
            "Lead Owner is not a User",
            "SubscriberKey",
            _SubscriberKey
        )
        RaiseError('Lead Owner is not a User', true)
    ENDIF
]%%
</td></tr></table>